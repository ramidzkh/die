plugins {
    id("com.diffplug.spotless") version "6.10.0" apply false
}

allprojects {
    group "me.ramidzkh.die"
    version "1.1.0"
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "com.diffplug.spotless"

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly("org.jetbrains:annotations:23.0.0")

        testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.0")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.9.0")
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        java {
            target("src/*/java/**/*.java")

            endWithNewline()
            indentWithSpaces()
            removeUnusedImports()
            toggleOffOn()
            eclipse().configFile(rootProject.file("codeformat/codeformat.xml"))
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)

                if (signing.signatory != null) {
                    signing.sign(this)
                }
            }
        }

        repositories {
            mavenLocal()

            maven {
                def releasesRepoUrl = uri("${buildDir}/repos/releases")
                def snapshotsRepoUrl = uri("${buildDir}/repos/snapshots")
                name = "Project"
                url = version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
